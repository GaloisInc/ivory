\section{Ivory Overview}
\label{sec:ivory-overview}

\lee{need lead-in here. What does Ivory do? What's the motivation for it's
  design (I like to motivate it with the JPL's Power of 10 rules).}

In this section, we give an illustrative overview of Ivory.  An Ivory
program is a Haskell program producing a collection of Ivory modules,
each module containing type and procedure definitions.  %% Type
%% definitions are produced using a quasi-quoter\cite{quoted}, while procedure
%% definitions are built from a set of monadic combinators.

Ivory is a staged language: the Haskell program compiles Ivory modules
to produce an AST which is then passed to one or more back-ends.  Thus,
an executable is produced from an Ivory program by compiling and
running the Haskell code to produce a C program, which is then
compiled with a C compiler.  Alternately, checking of pre- and
post-conditions is performed by running the Haskell program in
conjunction with the verification back-end.

In the following, we introduce both the types and values of Ivory programs but
postpone most discussion of the types to Section~\ref{sec:ivory-embedding}. We
focus on core aspects of the language in this introduction and throughout
the paper. Ivory contains a large number of operators and standard libraries we
elide. Examples include serialization, safe type casts, nullable pointers (for
inter-operation with legacy C), function pointers, and bit operators.

\subsection{Ivory Statements}

Ivory statements are terms in the \cd{Ivory} monad.  This monad
provides fresh variables, along with constructors for Ivory
statements. Unlike C, Ivory expressions must be pure, so
side-effecting operations take place at the statement level, in
the context of the monad. This ensures a defined order for effects,
eliminating large classes of unintuitive and undefined behaviors.

Memory in Ivory is manipulated via non-nullable references~\cite{memareas}.
References are read and written using the \cd{deref} and \cd{store}
statements, respectively.  For example, the following Haskell function
takes a reference to a signed 32-bit integer value and constructs
a program fragment which increments the value of the reference.

\begin{code}
incr_ref :: Ref s (Stored Sint32) -> Ivory eff ()
incr_ref r = do
    v <- deref r
    store r (v + 1)
\end{code}

\noindent
A reference in Ivory may refer either to a global object, allocated at
compile time, or a \emph{local} object, allocated dynamically.
Dynamic objects are created in ephemeral regions associated
with the scope of the containing procedure; operationally, local
objects are allocated on the stack, so regions are implicitly freed
on procedure return.  Ivory reference types are indexed by region
variables, the parameter \cd{s} seen in the type signatures above.
Along with type variable scoping, these region annotations on
references ensure that references do not escape the context in which
they were allocated.

The definition \cd{incr\_ref} is not a complete Ivory procedure.
Rather, it can be thought of as a template parameterised by a reference.
Ivory procedures must be explicitly defined and exported through
procedure definitions, such as
\begin{code}
incr_def :: Def ('[Ref s (Stored Sint32)] :-> Sint32)
incr_def = proc "incr_def" $ \r -> body $ do
  incr_ref r
  v <- deref r
  ret v
\end{code}
\noindent
The procedure \cd{incr\_def}, introduced by the use of \cd{proc}, calls the
Haskell function \cd{incr\_ref} above.  The use of \cd{body} indicates that the
procedure's definition will follow.  A more thorough treatment of \cd{proc} and
\cd{body} is given in section \ref{sec:proc}.  The type of \cd{incr\_def} makes
use of the data kinds extension, representing the argument list as a promoted
list.  The \cd{ret} statement returns a value from the Ivory procedure (we use
\cd{ret} rather than \cd{return} to avoid conflicting with Haskell's \cd{return}
function).

%% \nonident
%% The Ivory embedding uses standard Haskell techniques\cite{} to reuse
%% Haskell's binders to name procedure arguments.  These binders do not
%% appear in the AST, however, being erased by the \cd{proc} smart
%% constructor.

The Ivory monad tracks effects (the \cd{eff} type parameter); see
Section~\ref{sec:ivory-monad}.  One of these effects is the current allocation
region: the allocation function \cd{local} returns a reference that is tied to
that region.  For example, the following constructs a zero initialized reference
to an integer; the \cd{ival} constructs an initializer from a value:

\begin{code}
make_zero :: (GetAlloc eff %*\mytilde*) Scope s)
          => Ivory eff (Ref s (Stored Sint32))
make_zero = local (ival 0)
\end{code}

\subsection{Data Structures}

Ivory provides C-style arrays and structures.  Array types are
parameterised by their size and the type system ensures that array accesses are
within bounds. Structures are defined using a quasi-quoter to specify the
field names and their types. Arrays and structures belong to a special kind,
\cd{Area}, whose values are only ever manipulated through references.  In order
to lift the types of values that can be manipulated directly to \cd{Area}, the
\cd{Stored} type constructor is used.  Fields in structures are accessed
via the \cd{\mytilde>} operator which takes a reference to a struct and a field
name, and returns a reference at the field's type. For example, the following
code declares a structure type named \cd{position}, as well as a function that
will allocate an instance, and perform some basic operations on it.

\begin{code}
[ivory|
struct position
  { latitude  :: Stored IFloat
  ; longitude :: Stored IFloat
  ; altitude  :: Stored Sint32
  }
|]
struct_ex = do
  s <- local (istruct [ latitude  .= ival 45.52
                      , longitude .= ival (-122.68)
                      , altitude  .= ival 1524 ])
  lat <- deref (s %*\mytilde*)> latitude)
  lon <- deref (s %*\mytilde*)> longitude)
  incr_ref (s %*\mytilde*)> altitude)
\end{code}

\subsection{Control Structures}
\label{sec:control}

Ivory supports the usual control structures: the \cd{ifte\_} statement
constructor takes a boolean argument and two statements, one for each
branch of the if-then-else, while the pure ternary operator, \cd{?},
selects from two alternatives at the expression level.

\begin{code}
abs :: Def('[Sint16] :-> Sint16)
abs = proc "abs" $ \v -> body $ do
  ifte_ (v <? 0)
    (ret (-1*v))
    (ret v)

abs2 :: Def('[Sint16] :-> Sint16)
abs2 = proc "abs2" $ \v -> body $ do
  ret $ (v <? 0) ? ((-1*v), v)
\end{code}
%$
Ivory has two classes of iteration constructs: \cd{forever} for non-terminating
loops such as OS tasks, and loops with constant bounds. The prototypical
bounded loop in Ivory is the \cd{arrayMap}, which iterates over the elements of
an array. For example, the following
procedure adds \cd{x} to each element of the array \cd{arr}, noting
that \cd{arr ! ix} returns a \emph{reference} to the \cd{ix}-th
element of \cd{arr}.

\begin{code}
mapProc = proc "mapProc"
        $ \arr x -> body
        $ arrayMap
        $ \ix -> do
            v <- deref (arr ! (ix :: Ix 4))
            store (arr ! ix) (v + x :: Uint8)
\end{code}
%$
Note that we do not need to pass \cd{arr} to \cd{arrayMap} to determine the
correct bounds on the loop; rather, as we explain in \autoref{sec:area}, GHC can
\emph{infer} the bounds from the loop body!

\subsection{Assertions}

Ivory supports pre- and post-conditions, along with assertions.  The
Ivory compiler can emit run-time assertions to enforce these
conditions, or the model checker back-end can be used to statically
verify these properties hold.

\begin{code}
predicates_ex :: Def('[ IFloat ] :-> IFloat)
predicates_ex = proc "predicates_ex" $
    \i -> requires (i >? 0)
        $ ensures (\r -> r >? 0)
        $ body
        $ do (assert (i /=? 0))
             ret (i + 1))
\end{code}

\noindent
An \cd{ensures} clause takes a function, such that when applied to the return
value at any return point in the procedure, the predicate should hold.

\sjw{removed, what is the main point of this section?}
\jamey{The printf\_proc example illustrates this, but does it pass the sanity
checker? I don't think so...?}
\eric{Good point, the sanity checker will complain about the call to
  printf\_none, but I'd call this a shortcoming of the sanity checker. I guess
  it needs some notion of sum types for importProcs (and maybe externProcs?).}

% \begin{code}

% print_ex_module :: Module
% print_ex_module = package "print_ex" $ do
%   incl printf_none
%   incl printf_sint32
%   incl print_proc

% printf_none :: Def('[IString] :-> Sint32)
% printf_none  = importProc "printf" "stdio.h"

% printf_sint32 :: Def('[IString, Sint32] :-> Sint32)
% printf_sint32  = importProc "printf" "stdio.h"

% print_proc = Def('[]:->())
% print_proc = proc "print_proc" $ body $ do
%   _ <- call printf_none "hello, world!\n"
%   _ <- call printf_sint32 "print an integer: \%d" 42
%   return ()
% \end{code}

% Ivory can interact with externally defined C functions and global
% variables. The \cd{importProc} primitive allows the user to declare an external
% procedure, and ensures the correct header file is included by the generated
% code.

% Ivory can only import and use functions that have a valid Ivory type signature.
% Some polymorphic C functions may have multiple valid Ivory types.
% \jamey{The printf\_proc example illustrates this, but does it pass the sanity
% checker? I don't think so...?}

% \subsection{Toolchain Use}
% The Ivory compiler is a Haskell function that takes a list of \cd{Module}s,
% parses command line options, and writes generated C source and header files to
% a directory given by those options.

% The compiler's second argument is a list of \cd{Artifact}s. Artifacts are a
% datatype for an arbitrary Haskell string and a filename, indicating the contents
% of a non-Ivory-generated file to be written to the output directory. In
% practice, this is used to write Makefiles, native C sources, linker scripts, and debug output
% specified by the user.

% There are also related functions exposed to the user that allow the parsing
% of command line options to be separated from the compile step, where desired.

% \begin{code}
% import Ivory.Compile.C (compile)

% main :: IO ()
% main = compile [ ex_module, print_ex_module ] []
% \end{code}





